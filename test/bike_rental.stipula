stipula BikeRental {
	assets wallet
	fields cost, rentingTime, code
	init Inactive

	agreement(Lender, Borrower, Authority)(rentingTime, cost) { 
		Lender, Borrower: rentingTime, cost
	} => @Inactive

	@Inactive Lender: offer(x)[] { 
		x -> code
	} => @Payment

	@Payment Borrower: pay()[h] (h == cost) {
		h -o wallet 
		code -> Borrower
		now + rentingTime >> @Using {
			EndReached -> Borrower
		} => @Return 
	} => @Using

	@Using Borrower: end()[] { 
		"EndReached" -> Lender
	} => @Return

	@Return Lender: rentalOk()[] { 
		wallet -o Lender
	} => @End

	@Using Lender: dispute(x)[] { 
		x -> Authority
	} => @Dispute

	@Return Lender: dispute(x)[] { 
		x -> Authority
	} => @Dispute

	@Using Borrower: dispute(x)[] { 
		x -> Authority
	} => @Dispute

	@Return Borrower: dispute(x)[] { 
		x -> Authority
	} => @Dispute

	@Dispute Authority: verdict(x, y)[] (y >= 0 && y <= 1) {
		x -> Lender
		x -> Borrower 
		(y * wallet) -o wallet, Lender
		wallet -o Borrower
	} => @End
}
